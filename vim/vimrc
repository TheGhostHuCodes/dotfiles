" Vundle Settings {{{
set nocompatible 
filetype off
set runtimepath+=~/.vim/bundle/Vundle.vim
call vundle#begin()
Plugin 'gmarik/Vundle.vim'
Plugin 'tpope/vim-dispatch'
Plugin 'tmhedberg/SimpylFold'
Plugin 'altercation/vim-colors-solarized'
" Plugins in development.
Plugin 'file:///home/yhu/Repos/potion'
call vundle#end()
filetype plugin indent on
" }}}

" Basic Settings {{{
if has ("win32")
    " Making gvim legible on 4k screens.
    set guifont=Consolas:h12
    " Please stop dinging at me.
    set vb t_vb=
endif
if has ("unix")
    " For zsh settings.
    set term=screen-256color
endif
" Turn on Solarized color scheme.
syntax on
set background=dark
colorscheme solarized
set foldlevelstart=0
set tabstop=4
set shiftwidth=4
set expandtab
" Set relative line numbers for faster vertical movement.
set number
set relativenumber
set numberwidth=2
" Set for faster horizontal movement.
set sidescroll=10
set listchars+=precedes:<,extends:>
set nowrap
set autoindent
autocmd BufNewFile,BufRead *.md set filetype=markdown
set hlsearch incsearch
" }}}

" Mappings {{{
let mapleader = " "
let maplocalleader = "\\"
" Quick edit and source .vimrc file.
nnoremap <leader>rc :split $MYVIMRC<cr>
nnoremap <leader>src :source $MYVIMRC<cr>
" Toggle highlight for whitespace at the end of a line.
nnoremap <leader>w :match Error /\v\s+$/<cr>
nnoremap <leader>W :match none<cr>
" Always use very magic mode for searching to minimize Regex tears.
nnoremap / /\v
" Turn off highlighting items from the last search.
nnoremap <leader>/ :nohlsearch<cr>
" Cycle forward and backwards one buffer.
nnoremap <leader><tab> <C-W><C-W>
nnoremap <leader><S-tab> <C-W>W
" Quickfix List movement.
nnoremap <leader>cn :cnext<cr>
nnoremap <leader>cN :cprevious<cr>
nnoremap <leader>cc :cclose<cr>
" Re-flow paragraph.
nnoremap Q gqap
" Toggle spell checking.
nnoremap <f7> :setlocal spell! spelllang=en_us<cr>
" Toggle local line numbers and relative line numbers.
nnoremap <leader>N :setlocal number!<cr>
nnoremap <leader>RN :setlocal relativenumber!<cr>
" Opens previous buffer in an above split.
nnoremap <leader>st :execute "leftabove split " . bufname("#")<cr>
" Move current line down one line.
nnoremap <leader>- ddp
" Move current line up one line.
nnoremap <leader>_ kddpk
" Clears line and drops into insert mode.
nnoremap <leader>cl ddO
" Adds semicolon to end of current line without moving cursor.
nnoremap <leader>; :execute "normal! mqA;\e`q"<cr>
" Changes word at cursor to uppercase.
inoremap <c-u> <esc>viwUi
" Make surrounding characters managable.
inoremap ( ()<Esc>i
inoremap [ []<Esc>i
inoremap < <><Esc>i
inoremap { {<CR>}<Esc>O
inoremap " ""<Esc>i
inoremap ' ''<Esc>i
" Surround word under cursor in quotes.
nnoremap <leader>" viw<esc>a"<esc>hbi"<esc>lel
nnoremap <leader>' viw<esc>a'<esc>hbi'<esc>lel
" Surround selected text in quotes.
vnoremap <leader>' <esc>`>a'<esc>`<i'
vnoremap <leader>" <esc>`>a"<esc>`<i"
" Surround selected text in brackets.
vnoremap <leader>{ <esc>`>a}<esc>`<i{
vnoremap <leader>( <esc>`>a)<esc>`<i(
vnoremap <leader>< <esc>`>a><esc>`<i<
vnoremap <leader>[ <esc>`>a]<esc>`<i[
" Operators to search inside various brackets.
onoremap il( :<c-u>normal! F)vi(<cr>
onoremap in( :<c-u>normal! f)vi(<cr>
onoremap il{ :<c-u>normal! F}vi{<cr>
onoremap in{ :<c-u>normal! f}vi{<cr>
onoremap il[ :<c-u>normal! F]vi[<cr>
onoremap in[ :<c-u>normal! f]vi[<cr>
onoremap il< :<c-u>normal! F>vi<<cr>
onoremap in< :<c-u>normal! f>vi<<cr>
" Operator to search for the next email address.
onoremap in@ :<c-u>normal! f@F lvf@f.2w<cr>

nnoremap <leader>f :call <SID>FoldColumnToggle()<cr>
function! s:FoldColumnToggle()
    if &foldcolumn
        setlocal foldcolumn=0
    else
        setlocal foldcolumn=4
    endif
endfunction

nnoremap <leader>q :call <SID>QuickfixToggle()<cr>
let g:quickfix_is_open = 0
function! s:QuickfixToggle()
    if g:quickfix_is_open
        cclose
        let g:quickfix_is_open = 0
        execute g:quickfix_return_to_window . "wincmd w"
    else
        let g:quickfix_return_to_window = winnr()
        copen
        let g:quickfix_is_open = 1
    endif
endfunction
" }}}

" Abbreviations {{{
iabbrev @@ yungjinhu@gmail.com 
" }}}

" Status Line {{{
set statusline=%.20F        " Full path to file (20 character limit).
set statusline+=%=          " Switch to the right side.
set statusline+=%l/%L       " Current/Total line.
set statusline+=\ (%p%%)    " Current % in file.
" }}}

" FileType-specific settings {{{
" Html {{{
    augroup filetype_html
        autocmd!
        autocmd filetype html nnoremap <buffer> <localleader>f vatzf
    augroup end
" }}}

" Markdown {{{
augroup filetype_markdown
    autocmd!
    " The OR (\|) operator in an execute command is \\\| (ulgh!)
    autocmd FileType markdown onoremap <buffer> ih :<c-u>execute "normal! ?^==\\+\\\|--\\+$\r:nohlsearch\rkvg_"<cr>
    autocmd FileType markdown onoremap <buffer> ah :<c-u>execute "normal! ?^==\\+\\\|--\\+$\r:nohlsearch\rg_vk0"<cr>
augroup END
" }}}

" Vimscript {{{
augroup filetype_vim
    autocmd!
    autocmd FileType vim setlocal foldmethod=marker
augroup END
" }}}
" }}}
